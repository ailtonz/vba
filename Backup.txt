###
###
###
### Category1 : Function
### Category2 : 
### Category3 : 
### Description : 
###
###
###
Public Function Backup(sOrigem As String, sDestino As String, sArquivo As String, sExtencao As String)
'===================================================================
'   Funções agregadas a esta função:
'   Public Function CompactarRepararDatabase(DatabasePath As String, Optional Password As String, Optional TempFile As String = "c:\tmp.mdb")
'   Public Function CriarPasta(sPasta As String) As String
'
'===================================================================
'sOrigem = "c:\PastaDoSistema"
'sDestino = "NomeDaPastaDeBackup"
'sArquivo = "dbDados"
'sExtencao = ".MDB"

Dim oSHL
Dim oFSO As New FileSystemObject
Dim oPasta As New FileSystemObject
Dim tmp, p1, p2, p3, p4, p5
Dim Origem As String

On Error Resume Next
Err.Clear

Origem = Application.CurrentProject.Path & "\" & sArquivo & sExtencao

'Começa o bkp se o arquivo existir na origem
If Dir(Origem) <> "" Then
   
    p1 = Right("00" & Year(Now()), 2)
    p2 = Right("00" & Month(Now()), 2)
    p3 = Right("00" & Day(Now()), 2)
    p4 = Right("00" & Hour(Now()), 2)
    p5 = Right("00" & Minute(Now()), 2)
     
    tmp = ("_" & p3 & p2 & p1 & "_" & p4 & p5)
    
    CompactarRepararDatabase sOrigem & "\" & sArquivo & sExtencao
    
    sOrigem = sOrigem & "\"
    
    oFSO.CopyFile sOrigem & sArquivo & sExtencao, CriarPasta(sDestino) & sArquivo & tmp & sExtencao, True
    
    If Err <> 0 Then
        MsgBox "Error: " & Err & " " & Err.Description, vbExclamation, "Backup"
    Else
        Set oSHL = CreateObject("WScript.Shell")
        oSHL.PopUp "Backup completo!", 1, "Backup", 0 + 64
    End If
    
Else
    
    MsgBox "ATENÇÃO: Execute esta operação apartir do computador que contém os dados do sistema", vbInformation + vbOKOnly, "Backup"
    
End If

End Function

Public Function CriarPasta(sPasta As String) As String
'===================================================================
'   Cria pasta apartir da origem do sistema
'===================================================================

Dim fPasta As New FileSystemObject
Dim MyApl As String

MyApl = Application.CurrentProject.Path
        
If Not fPasta.FolderExists(MyApl & "\" & sPasta) Then
   fPasta.CreateFolder (MyApl & "\" & sPasta)
End If

CriarPasta = MyApl & "\" & sPasta & "\"

End Function

Public Function CompactarRepararDatabase(DatabasePath As String, Optional Password As String, Optional TempFile As String = "c:\tmp.mdb")

'===================================================================
' Se a versao DAO for anterior a 3.6 , entao devemos usar o método RepairDatabase
' Se a versao DAO for a 3.6 ou superior basta usar a função CompactDatabase
'===================================================================

If DBEngine.Version < "3.6" Then DBEngine.RepairDatabase DatabasePath

'se nao informou um arquivo temporario usa "c:\tmp.mdb"
If TempFile = "" Then TempFile = "c:\tmp.mdb"

'apaga o arquivo temp se existir
If Dir(TempFile) <> "" Then Kill TempFile

'formata a senha no formato ";pwd=PASSWORD" se a mesma existir
If Password <> "" Then Password = ";pwd=" & Password

'compacta a base criando um novo banco de dados
DBEngine.CompactDatabase DatabasePath, TempFile, , , Password

'apaga o primeiro banco de dados
Kill DatabasePath

'move a base compactada para a origem
FileCopy TempFile, DatabasePath

'apaga o arquivo temporario
Kill TempFile

End Function
